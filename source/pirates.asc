'
' Pirates Bounty 
' (C) 2018 Robin Southern
' github.com/betajaen
'
K_NB_ICONS=2
K_ARMY_PIRATES=1
K_ARMY_MONKEYS=2
K_ARMY_MERCS=3
K_ARMY_HENCHMEN=4
K_ARMY_VOODOO=5
K_ARMY_GHOSTS=6
K_ARMY_ZOMBIES=7
K_ARMY_SKELETONS=8
K_ARMY_REDCOATS=9
K_ARMY_COUNT=10

Global K_ARMY_PIRATES,K_ARMY_MONKEYS,K_ARMY_MERCS,K_ARMY_HENCHMEN
Global K_ARMY_VOODOO,K_ARMY_GHOSTS,K_ARMY_ZOMBIES,K_ARMY_SKELETONS
Global K_ARMY_REDCOATS

Dim K_TYPE_STR$(K_ARMY_COUNT)
Global K_TYPE_STR$()

Dim K_ATTACK_EFFECTIVE(K_ARMY_COUNT)
Dim K_ATTACK_DAMAGE(K_ARMY_COUNT)
Dim K_ATTACK_DEFENCE(K_ARMY_COUNT)

Global K_ATTACK_EFFECTIVE(),K_ATTACK_DAMAGE(),K_ATTACK_DEFENCE()

' Effective, Defence, Damage 
Proc _SETATTACK[K_ARMY_PIRATES,"Pirates",18,18,2]
Proc _SETATTACK[K_ARMY_MONKEYS,"Monkeys",5,9,1]
Proc _SETATTACK[K_ARMY_MERCS,"Mercs",7,6,4]
Proc _SETATTACK[K_ARMY_HENCHMEN,"Henchmen",7,5,3]

K_OBJ_PLAYER=1
K_OBJ_ENEMY=2

Global K_OBJ_PLAYER,K_OBJ_ENEMY,K_OBJ_TOWN,K_RECRUIT,K_OBJ_FORT

K_NORTH=1
K_EAST=2
K_SOUTH=3
K_WEST=4

Global K_NORTH,K_EAST,K_SOUTH,K_WEST

' TILETYPES

K_TILE_WALK=0
K_TILE_BLOCK=1
K_TILE_DOCK=2
K_TILE_TOWN=3
K_TILE_FORT=4
K_TILE_TREASURE=5
K_TILE_RECRUIT=6
K_TILE_ISLAND=7
K_TILE_BURIEDTREASURE=8

Global K_TILE_WALK,K_TILE_BLOCK,K_TILE_DOCK,K_TILE_TOWN,K_TILE_FORT
Global K_TILE_TREASURE,K_TILE_RECRUIT,K_TILE_ISLAND,K_TILE_BURIEDTREASURE


' TOWN Actions 
K_TOWN_TREASURE=1

Global K_TOWN_TREASURE

' Redraw the screen according to state 
_REDRAW=False
' Evaluate AI/Next Turn
_EVAL=False

Global _REDRAW,_EVAL

_STATE=1
_X=0
_Y=0
_X1=0
_Y1=0

Global _STATE
Global _MAPVIEW_X
Global _MAPVIEW_Y
Global _MAPVIEW_W
Global _MAPVIEW_H
Global _MAP_X
Global _MAP_Y

' 0 = World Map
' 1 = Island Map 
' 2 = Battle Map 
Global _MAPMODE
_UIMODE=0
Global _UIMODE

' 0 = World Map
' 1 = Rum Island 
' 2 = Oyster Cay 
' 3 = Barracuda Isle 
' 4 = Danger Cove  

_ISLAND=0
Global _ISLAND
_PLAYER_NAME$=""
_PLAYER_GOLD=0
_PLAYER_REPUTATION=0
_PLAYER_X=0
_PLAYER_Y=0
_PLAYER_MOVE=0
Global _PLAYER_NAME$,_PLAYER_GOLD,_PLAYER_REPUTATION,_PLAYER_X,_PLAYER_Y,_PLAYER_MOVE

' Player Army
Dim _PLAYER_ARMY_TYPE(6),_PLAYER_ARMY_SIZE(6),_PLAYER_ARMY_OBJ(6)
Global _PLAYER_ARMY_TYPE(),_PLAYER_ARMY_SIZE(),_PLAYER_ARMY_OBJ()

' Enemy Army 
Dim _ENEMY_ARMY_TYPE(6),_ENEMY_ARMY_SIZE(6),_ENEMY_ARMY_OBJ(6)
Global _ENEMY_ARMY_TYPE(),_ENEMY_ARMY_SIZE(),_ENEMY_ARMY_OBJ()

' Army/Battle
_ARMY_TURN=0
Dim _ARMY_SELECT(2)
Global _ARMY_TURN,_ARMY_SELECT()

' Fort 
_FORT_BATTLE=False

Global _FORT_BATTLE


' Recruit
_RECRUIT_TYPE=K_ARMY_PIRATES
_RECRUIT_COUNT=100
_RECRUIT_COST=5
_RECRUIT_ENLIST=0

' TOWN 

_TOWN_NAME$="Southern Port"
_TOWN_TREASURE=True
_TOWN_ACTION=0

Global _TOWN_NAME$,_TOWN_TREASURE,_TOWN_ACTION

Global _RECRUIT_TYPE,_RECRUIT_COUNT,_RECRUIT_COST,_RECRUIT_ENLIST

' TREASURE 
_TREASURE_GOLD=0
Global _TREASURE_GOLD

' Log
Dim _LOG$(4)
Global _LOG$()

' Objects
Dim _OBJ_TYPE(32),_OBJ_BOB(32),_OBJ_TIMER(32),_OBJ_X(32),_OBJ_Y(32),_OBJ_FLAGS(32)
Global _OBJ_TYPE(),_OBJ_X(),_OBJ_Y(),_OBJ_BOB(),_OBJ_FLAGS(),_OBJ_TIMER()

' Pushed/Popped Objects
Dim _POBJ_TYPE(32),_POBJ_BOB(32),_POBJ_TIMER(32),_POBJ_X(32),_POBJ_Y(32),_POBJ_FLAGS(32)
Global _POBJ_TYPE(),_POBJ_BOB(),_POBJ_TIMER(),_POBJ_X(),_POBJ_Y(),_POBJ_FLAGS()

'Default Palette $FFF,$0,$F00,$F0,$F 
Screen Open 0,320,256,8,Lowres
Curs Off 
Flash Off 
Reserve Zone 1
Reserve Zone 2
Reserve Zone 3
Reserve Zone 4
Reserve Zone 5
Reserve Zone 6

Cls 5

' Load in foreground as bobs 
Load "Pirates:resources/foreground.abk"

' Load main 16x16 sprites/icons
Load "Pirates:resources/tiles.abk"
Get Icon Palette 

' Append to troll font to sprites/icons
Load "Pirates:resources/troll.abk",1

' Append UI to sprites/icons 
Load "Pirates:resources/ui.abk",1

' Make the "pink transparent" as a white 
Palette $FFF,


' Make the font parts transparent
For I=257 To Length(2)
   Make Icon Mask I
Next I

Cls 0

'Proc _BUTTON[10,10,32,"Test"] 
'End 


' Load the map 
L=Map Length(100,100)
Reserve As Data 6,L
Bload "Pirates:resources/map.map",Start(6)

Reserve As Data 8,2048
Bload "Pirates:resources/map.map.val",Start(8)

' Set map view port
Tile Size 16,16

_PLAYER_NAME$="Blackbeard"
_PLAYER_GOLD=1000
_PLAYER_REPUTATION=1
_PLAYER_X=0
_PLAYER_Y=0
_PLAYER_MOVE=0
_PLAYER_MAP_PIECES=0

Global _PLAYER_MAP_PIECES

_PLAYER_ARMY_TYPE(0)=K_ARMY_PIRATES
_PLAYER_ARMY_SIZE(0)=20

Load Iff "Pirates:resources/title.iff"
Palette $FFF,

Wait Key 

Cls 0

Map View 0,0 To 224,224


Proc _GOTOISLAND[1]

Goto GAMESTART

'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'
'      FAST GOSUBS 
'
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

_GFX_TEXT_X=0 : _GFX_TEXT_Y=0 : _GFX_TEXT_STR$="" : _GFX_TEXT_NUM=0

Global _GFX_TEXT_X,_GFX_TEXT_Y,_GFX_TEXT_STR$,_GFX_TEXT_NUM


_DO_GFX_TEXT_STR:
' TODO 
Return 

Procedure _DO_GFX_TEXT_NUM
   
   If _GFX_TEXT_NUM=0
      Paste Icon _GFX_TEXT_X,_GFX_TEXT_Y,257+48-33
   Else 
      
      
      ' TODO 
      If _GFX_TEXT_NUM>9999
         _GFX_TEXT_X=_GFX_TEXT_X+5*5
      Else If _GFX_TEXT_NUM>999
         _GFX_TEXT_X=_GFX_TEXT_X+5*4
      Else If _GFX_TEXT_NUM>99
         _GFX_TEXT_X=_GFX_TEXT_X+5*3
      Else If _GFX_TEXT_NUM>9
         _GFX_TEXT_X=_GFX_TEXT_X+5*2
      End If 
      
      While _GFX_TEXT_NUM>0
         ___MOD=_GFX_TEXT_NUM mod 10
         _GFX_TEXT_NUM=_GFX_TEXT_NUM/10
         Paste Icon _GFX_TEXT_X,_GFX_TEXT_Y,257+48-33+___MOD
         _GFX_TEXT_X=_GFX_TEXT_X-5
      Wend 
   End If 
End Proc

Procedure _PRINTTROLL[X,Y,TXT$]
   L=Len(TXT$)
   For I=1 To L
      C$=Mid$(TXT$,I,1)
      A=Asc(C$)
      If A>32
         Paste Icon X,Y,257+A-33
      End If 
      If A<46
         X=X+2
      Else If A<57
         If A<50
            X=X+4
         Else 
            X=X+5
         End If 
      Else If A<64
         X=X+6
      Else If A>92
         If A=105 or A=106 or A=108 or A=116
            X=X+4
         Else 
            X=X+6
         End If 
      Else 
         If A=87 or A=77
            X=X+9
         Else 
            X=X+8
         End If 
      End If 
   Next I
End Proc


'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
'
'
'      GAME MODES
'
'
'''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

' World Map
_WORLDMAP:

'62
'45 47 
'30
If _SC=62 Then _PLAYERMOVE[K_NORTH]
If _SC=45 Then _PLAYERMOVE[K_WEST]
If _SC=47 Then _PLAYERMOVE[K_EAST]
If _SC=30 Then _PLAYERMOVE[K_SOUTH]

If _EVAL=True
   _REDRAW=True
   _EVAL=False
   
   Proc _UPDATEWORLD
   
   If _STATE<>1
      Return 
   End If 
   
End If 

If _REDRAW=True
   _REDRAW=False
   Cls 0,0,200 To 320,256
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
   Proc _DRAWLOG
End If 

Return 

' Island Map 
_ISLANDMAP:

' Num Keys 
'62
'45 47 
'30
If _SC=62 Then _PLAYERMOVE[K_NORTH]
If _SC=45 Then _PLAYERMOVE[K_WEST]
If _SC=47 Then _PLAYERMOVE[K_EAST]
If _SC=30 Then _PLAYERMOVE[K_SOUTH]

' F1 
If _SC=80
   _REDRAW=True
End If 

If _EVAL=True
   _REDRAW=True
   _EVAL=False
   
   Proc _UPDATEISLAND
   
   If _STATE<>1 or _GAMEMODE<>2
      Return 
   End If 
   
End If 

If _REDRAW=True
   _REDRAW=False
   Cls 0,0,200 To 252,256
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
   Proc _DRAWLOG
   Proc _DRAWUI
End If 

Return 

'''''''''''''''''''''''''''''''''''''''''''''''''''''' 
'
'
'           Battle Map 
'
'  
'''''''''''''''''''''''''''''''''''''''''''''''''''''' 
_BATTLEMAP:

'62
'45 47 
'30
If _SC=62 Then _PLAYERMOVE[K_NORTH]
If _SC=45 Then _PLAYERMOVE[K_WEST]
If _SC=47 Then _PLAYERMOVE[K_EAST]
If _SC=30 Then _PLAYERMOVE[K_SOUTH]

If _EVAL=True
   Proc _UPDATEBATTLE
   
   _REDRAW=True
   _EVAL=False
   
   If _STATE<>3
      Return 
   End If 
End If 

If _REDRAW=True
   _REDRAW=False
   Cls 0,0,0 To 200,200
   Cls 0,0,200 To 320,256
   
   Proc _DRAWOBJECTS
   
   For I=0 To 6
      If _PLAYER_ARMY_TYPE(I)<>0
         OB=_PLAYER_ARMY_OBJ(I)
         _GFX_TEXT_X=_OBJ_X(OB)*16+18
         _GFX_TEXT_Y=_OBJ_Y(OB)*16+12
         _GFX_TEXT_NUM=_PLAYER_ARMY_SIZE(I)
         Proc _DO_GFX_TEXT_NUM
         
         '_PRINTTROLL[_OBJ_X(OB)*16+8,_OBJ_Y(OB)*16+8,"Test"] 
      End If 
   Next I
   
   For I=0 To 6
      If _ENEMY_ARMY_TYPE(I)<>0
         OB=_ENEMY_ARMY_OBJ(I)
         _GFX_TEXT_X=_OBJ_X(OB)*16+18
         _GFX_TEXT_Y=_OBJ_Y(OB)*16+12
         _GFX_TEXT_NUM=_ENEMY_ARMY_SIZE(I)
         Proc _DO_GFX_TEXT_NUM
         
         '_PRINTTROLL[_OBJ_X(OB)*16+8,_OBJ_Y(OB)*16+8,"Test2"]
      End If 
   Next I
   
   '   Proc _PRINTTROLL[30,30,"Battle!"]
   Proc _DRAWLOG
End If 

Return 

'''''''''''''''''''''''''''''''''''''''''''''''''
'
'         UI 
'
'
'''''''''''''''''''''''''''''''''''''''''''''''''

Procedure _DRAWUI
   Ink 1
   Draw 254,-1 To 254,256
   'Paste Icon 256,1,355

   Paste Icon 256,10,348

   _GFX_TEXT_X=263
   _GFX_TEXT_Y=9
   _GFX_TEXT_NUM=_PLAYER_GOLD
   
   Proc _DO_GFX_TEXT_NUM

   Paste Icon 256,20,349

   _GFX_TEXT_X=267
   _GFX_TEXT_Y=20
   _GFX_TEXT_NUM=_PLAYER_REPUTATION

   Proc _DO_GFX_TEXT_NUM

   Paste Icon 270,206,351

End Proc

_RECRUITMENT:

If _REDRAW
   _REDRAW=False
   Cls 0
   _PRINTTROLL[20,20,"There are men willing to join our cause!"]
   _PRINTTROLL[50,50,K_TYPE_STR$(_RECRUIT_TYPE)]
   
   _GFX_TEXT_X=20
   _GFX_TEXT_Y=50
   _GFX_TEXT_NUM=_RECRUIT_COUNT
   
   Proc _DO_GFX_TEXT_NUM
   
   _GFX_TEXT_X=130
   _GFX_TEXT_Y=185
   _GFX_TEXT_NUM=_RECRUIT_ENLIST
   
   Proc _DO_GFX_TEXT_NUM
   
   Proc _BUTTON_MINUS[1,100,180]
   Proc _BUTTON_PLUS[2,158,180]
   
   Proc _BUTTON_YES[3,200,180]
   Proc _BUTTON_NO[4,181,180]
   
   
End If 

If(Mouse Zone=1 and Mouse Click=1) or _SC=74
   _RECRUIT_ENLIST=Max(_RECRUIT_ENLIST-1,0)
   _REDRAW=True
Else If(Mouse Zone=2 and Mouse Click=1) or _SC=94
   _RECRUIT_ENLIST=Min(_RECRUIT_ENLIST+1,_RECRUIT_COUNT)
   _REDRAW=True
Else If(Mouse Zone=3 and Mouse Click=1) or _SC=67
   ' Do it
   Proc _LEAVERECRUIT
Else If(Mouse Zone=4 and Mouse Click=1) or _SC=60
   ' Cancel 
   _RECRUIT_ENLIST=0
   Proc _LEAVERECRUIT
End If 


Return 

_TOWN:
If _REDRAW
   _REDRAW=False
   Cls 0
   _PRINTTROLL[20,20,"This is the Town of"]
   _PRINTTROLL[50,50,_TOWN_NAME$]

   If _TOWN_TREASURE=True

   _PRINTTROLL[50,100,"Do you want to buy a map?"]
   
   Proc _BUTTON_YES[1,200,180]
   End If 
   Proc _BUTTON_NO[2,181,180]
   
   
End If 

If _TOWN_TREASURE=True and((Mouse Zone=1 and Mouse Click=1) or _SC=67)
   ' Do it
   _TOWN_ACTION=1
   Proc _LEAVETOWN
Else If(Mouse Zone=2 and Mouse Click=1) or _SC=60
   ' Cancel 
   _TOWN_ACTION=0
   'K_TOWN_TREASURE 
   Proc _LEAVETOWN
End If 

Return 

_TREASURE:
If _REDRAW
   _REDRAW=False
   Cls 0
   _PRINTTROLL[20,20,"You've dug up some Treasure"]
   
   _GFX_TEXT_X=20
   _GFX_TEXT_Y=50
   _GFX_TEXT_NUM=_TREASURE_GOLD
   
   Proc _DO_GFX_TEXT_NUM
   Proc _BUTTON_YES[1,200,180]
End If 

If(Mouse Zone=1 and Mouse Click=1) or _SC=67
   Proc _LEAVETREASURE
End If 

Return 


'''''''''''''''''''''''''''''''''''''''''''''''''
'
'       COMMON PROCEDURES
'
'''''''''''''''''''''''''''''''''''''''''''''''''

ROLL=0
Global ROLL

Procedure _3D6
   ROLL=0
   For I=0 To 3
      ROLL=ROLL+1+Rnd(6)
   Next I
End Proc

Procedure _ATTACKROLL[SRC_TYPE,SRC_SIZE,TGT_TYPE,TGT_SIZE]
   NEW_SIZE=0
   
   
   EFFECTIVE=K_ATTACK_EFFECTIVE(SRC_TYPE)
   DEFENCE=K_ATTACK_DEFENCE(TGT_TYPE)
   DAMAGE=K_ATTACK_DAMAGE(SRC_TYPE)
   
   ' Roll for Effective 
   Proc _3D6
   
   If ROLL<=EFFECTIVE
      ' Roll for defence 
      Proc _3D6
      If ROLL>DEFENCE
         ' Damage 
         NEW_SIZE=Max(SRC_SIZE*DAMAGE,0)
      End If 
   End If 
   
End Proc[NEW_SIZE]

Procedure _SETATTACK[_TYPE,NAME$,_EFFECTIVE,_DEFENCE,_DAMAGE]
   K_ATTACK_EFFECTIVE(_TYPE)=_EFFECTIVE
   K_ATTACK_DEFENCE(_TYPE)=_DEFENCE
   K_ATTACK_DAMAGE(_TYPE)=_DAMAGE
   K_TYPE_STR$(_TYPE)=NAME$
End Proc

SX=0
SY=0
TX=0
TY=0

Global SX,SY,TX,TY

Procedure _SCREEN2TILE
   TX=Min(Max(_MAP_X+SX/16,0),Map X-1)
   TY=Min(Max(_MAP_Y+SY/16,0),Map Y-1)
End Proc

TILETYPE=0
Global TILETYPE

Procedure _TILETYPE
   TILETYPE=Tile Val(TX,TY,0)
End Proc

Procedure _DRAWLOG
   Y=225
   For I=0 To 3
      If Len(_LOG$(I))>0
         _PRINTTROLL[0,Y,_LOG$(I)]
         Y=Y+9
      End If 
   Next I
End Proc

Procedure _LOGTEXT[TX$]
   _LOG$(2)=_LOG$(1)
   _LOG$(1)=_LOG$(0)
   _LOG$(0)=TX$
End Proc

Procedure _BUTTON[X,Y,W,TX$]
   Paste Icon X,Y,352
   DV=W/16
   XX=X+8
   For I=1 To DV
      Paste Icon XX,Y,353
      XX=XX+16
   Next I
   Paste Icon XX,Y,354
   
   _PRINTTROLL[X+8,Y+5,TX$]
   
End Proc

Procedure _BUTTON_YES[ID,X,Y]
   Paste Icon X,Y,360
   Set Zone ID,X,Y To X+18,Y+18
End Proc

Procedure _BUTTON_NO[ID,X,Y]
   Paste Icon X,Y,359
   Set Zone ID,X,Y To X+18,Y+18
End Proc

Procedure _BUTTON_PLUS[ID,X,Y]
   Paste Icon X,Y,357
   Set Zone ID,X,Y To X+18,Y+18
End Proc
Procedure _BUTTON_MINUS[ID,X,Y]
   Paste Icon X,Y,358
   Set Zone ID,X,Y To X+18,Y+18
End Proc


Procedure _LOGNUM[NUM]
   Proc _LOGTEXT[Str$(NUM)]
End Proc

Procedure _LOGTEXTNUM[TX$,NUM]
   Proc _LOGTEXT[TX$+Str$(NUM)]
End Proc

Procedure _LOGTEXTNUM2[TX$,NUM,TX2$]
   Proc _LOGTEXT[TX$+Str$(NUM)+TX2$]
End Proc

Procedure _DRAWTILES
   Shared _MAPVIEW
   
   'Map View _MAPVIEW_X,_MAPVIEW_Y To _MAPVIEW_X+_MAPVIEW_W,_MAPVIEW_Y+_MAPVIEW_H 
   Map Do _MAP_X,_MAP_Y
End Proc

Procedure _DRAWOBJECTS
   For I=0 To 32
      If _OBJ_TYPE(I)>0
         Paste Bob _OBJ_X(I)*16,_OBJ_Y(I)*16,_OBJ_BOB(I)
      End If 
   Next I
End Proc

Procedure _CLEAROBJECTS
   For I=0 To 32
      _OBJ_TYPE(I)=0
   Next I
End Proc

Procedure _PLAYERMOVE[__DIR]
   _PLAYER_MOVE=__DIR
   _EVAL=True
End Proc

Procedure _PUSHOBJECTS
   For I=0 To 32
      _POBJ_TYPE(I)=_OBJ_TYPE(I)
      _POBJ_BOB(I)=_OBJ_BOB(I)
      _POBJ_X(I)=_OBJ_X(I)
      _POBJ_Y(I)=_OBJ_Y(I)
      _POBJ_FLAGS(I)=_OBJ_FLAGS(I)
      _POBJ_TIMER(I)=_OBJ_TIMER(I)
   Next I
   
End Proc

Procedure _POPOBJECTS
   For I=0 To 32
      _OBJ_TYPE(I)=_POBJ_TYPE(I)
      _OBJ_BOB(I)=_POBJ_BOB(I)
      _OBJ_X(I)=_POBJ_X(I)
      _OBJ_Y(I)=_POBJ_Y(I)
      _OBJ_FLAGS(I)=_POBJ_FLAGS(I)
      _OBJ_TIMER(I)=_POBJ_TIMER(I)
   Next I
End Proc

Procedure _ADDOBJECT[__OBJTYPE,__X,__Y,__BOB]
   ID=99
   For I=0 To 32
      If _OBJ_TYPE(I)=0
         _OBJ_TYPE(I)=__OBJTYPE
         _OBJ_BOB(I)=__BOB
         _OBJ_X(I)=__X
         _OBJ_Y(I)=__Y
         _OBJ_FLAGS(I)=0
         _OBJ_TIMER(I)=0
         ID=I
         _REDRAW=True
         Exit 
      End If 
   Next I
End Proc[ID]

Procedure _REMOVEOBJECT[__ID]
   _OBJ_TYPE(__ID)=0
   _REDRAW=True
End Proc[ID]

___MOVE_COORDS_X=0
___MOVE_COORDS_Y=0

Global ___MOVE_COORDS_X,___MOVE_COORDS_Y

Procedure _MOVECOORDS[__ID,__DIR]
   ___MOVE_COORDS_X=_OBJ_X(__ID)
   ___MOVE_COORDS_Y=_OBJ_Y(__ID)
   
   If __DIR=K_NORTH
      ___MOVE_COORDS_Y=___MOVE_COORDS_Y-1
   Else If __DIR=K_EAST
      ___MOVE_COORDS_X=___MOVE_COORDS_X+1
   Else If __DIR=K_SOUTH
      ___MOVE_COORDS_Y=___MOVE_COORDS_Y+1
   Else If __DIR=K_WEST
      ___MOVE_COORDS_X=___MOVE_COORDS_X-1
   End If 
   
End Proc

Procedure _DIRECTIONTO[__SX,__SY,__TX,__TY]
   _DIR=0
   
   If __SX>__TX
      _DIR=K_WEST
   Else If __SX<__TX
      _DIR=K_EAST
   Else If __SY>__TY
      _DIR=K_NORTH
   Else If __SY<__TY
      _DIR=K_SOUTH
   End If 
   
End Proc[_DIR]

Procedure _MANHATTANTO[__SX,__SY,__TX,__TY]
   _DIST=Abs(__TX-__SX)+Abs(__TY-__SY)
End Proc[_DIST]


MOVETYPE=0
Global MOVETYPE

Procedure _CANMOVE[__ID,__TGTX,__TGTY]
   CAN_MOVE=False
   
   TX=_MAP_X+__TGTX
   TY=_MAP_Y+__TGTY
   Proc _TILETYPE
   MOVETYPE=TILETYPE
   
   If TILETYPE=0 or TILETYPE=8
      CAN_MOVE=True
   End If 
   
End Proc[CAN_MOVE]

Procedure _OBJMOVE[__ID,__DIR]
   Proc _MOVECOORDS[__ID,__DIR]
   Proc _CANMOVE[__ID,___MOVE_COORDS_X,___MOVE_COORDS_Y]
   If Param=True
      _OBJ_X(__ID)=___MOVE_COORDS_X
      _OBJ_Y(__ID)=___MOVE_COORDS_Y
      _REDRAW=True
      _EVAL=True
   End If 
End Proc

Procedure _UPDATEWORLD
   
   PX=_OBJ_X(0)
   PY=_OBJ_Y(0)
   
   For I=1 To 32
      If _OBJ_TYPE(I)=K_OBJ_ENEMY
         Proc _DIRECTIONTO[_OBJ_X(I),_OBJ_Y(I),PX,PY]
         If Param<>0
            Proc _OBJMOVE[I,Param]
         End If 
         
         ' Future: Ship vs Ship combat  
         
         'If PX=_OBJ_X(I) and PY=_OBJ_Y(I)
         '   _GOTOBATTLE[I] 
         '   Goto _END
         'End If  
         
      End If 
   Next I
   
   If _PLAYER_MOVE<>0
      _OBJMOVE[0,_PLAYER_MOVE]
      _PLAYER_MOVE=0
      
      If MOVETYPE<>0
         If MOVETYPE=K_TILE_ISLAND
            Proc _GOTOISLAND[1]
            Goto _END
         End If 
      End If 
      
   End If 
   
   _END:
   ' Prevent eval loop
   _EVAL=False
End Proc

Procedure _UPDATEISLAND
   
   PX=_OBJ_X(0)
   PY=_OBJ_Y(0)
   
   For I=1 To 32
      If _OBJ_TYPE(I)=K_OBJ_ENEMY
         Proc _DIRECTIONTO[_OBJ_X(I),_OBJ_Y(I),PX,PY]
         If Param<>0
            Proc _OBJMOVE[I,Param]
         End If 
         
         If PX=_OBJ_X(I) and PY=_OBJ_Y(I)
            _GOTOBATTLE[I]
            Goto _END
         End If 
         
      End If 
   Next I
   
   If _PLAYER_MOVE<>0
      _OBJMOVE[0,_PLAYER_MOVE]
      _PLAYER_MOVE=0
      
      If MOVETYPE<>0
         If MOVETYPE=K_TILE_DOCK
            Proc _LEAVEISLAND
            Goto _END
         Else If MOVETYPE=K_TILE_RECRUIT
            Proc _ENTERRECRUIT
            Goto _END
         Else If MOVETYPE=K_TILE_FORT
            Proc _ENTERFORT
            Goto _END
         Else If MOVETYPE=K_TILE_TOWN
            Proc _ENTERTOWN
            Goto _END
         Else If MOVETYPE=K_TILE_TREASURE
            Map Plot 0,TX,TY
            
            Proc _ENTERTREASURE
            Goto _END
         End If 
      End If 
      
      
   End If 
   
   _END:
   ' Prevent eval loop
   _EVAL=False
End Proc

Procedure _DOPLAYER2ENEMYATTACK[PL,EN]
   ' Fight Player to Enemy
   Proc _ATTACKROLL[_PLAYER_ARMY_TYPE(PL),_PLAYER_ARMY_SIZE(PL),_ENEMY_ARMY_TYPE(EN),_ENEMY_ARMY_SIZE(EN)]
   _ENEMY_ARMY_SIZE(EN)=Max(_ENEMY_ARMY_SIZE(EN)-Param,0)
   _PLAYER_REPUTATION=_PLAYER_REPUTATION+Param

   If _ENEMY_ARMY_SIZE(EN)=0
      _REMOVEARMYOBJECT[False,EN]
      Proc _LOGTEXT["They have been defeated!"]
   Else If Param>0
      Proc _LOGTEXTNUM2["We hit the enemy for ",Param," men"]
   End If 
End Proc

Procedure _DOENEMY2PLAYERATTACK[EN,PL]
   ' Fight Enemy to Player! 
   Proc _ATTACKROLL[_ENEMY_ARMY_TYPE(EN),_ENEMY_ARMY_SIZE(EN),_PLAYER_ARMY_TYPE(PL),_PLAYER_ARMY_SIZE(PL)]
   _PLAYER_ARMY_SIZE(PL)=Max(_PLAYER_ARMY_SIZE(PL)-Param,0)
   
   If _PLAYER_ARMY_SIZE(PL)=0
      _REMOVEARMYOBJECT[True,PL]
      Proc _LOGTEXT["We have been defeated!"]
   Else If Param>0
      Proc _LOGTEXTNUM2["We lost ",Param," men"]
   End If 
End Proc


Procedure _UPDATEBATTLE
   
   ' Player Turn
   If _PLAYER_MOVE<>0
      OB=_ARMY_SELECT(0)
      _OBJMOVE[_PLAYER_ARMY_OBJ(OB),_PLAYER_MOVE]
      _PLAYER_MOVE=0
   End If 
   
   ' Enemy Turn 
   IDX=_ARMY_SELECT(1)
   OB=_ENEMY_ARMY_OBJ(IDX)
   TGTOB=0
   TGTIDX=0
   
   ' Go through all players and calc distances, if 1
   ' we attack. 
   For I=0 To 6
      If _ENEMY_ARMY_TYPE(I)=0 or _ENEMY_ARMY_SIZE(I)=0
         Goto L2
      End If 
      
      TGTOB=_PLAYER_ARMY_OBJ(I)
      
      Proc _MANHATTANTO[_OBJ_X(OB),_OBJ_Y(OB),_OBJ_X(TGTOB),_OBJ_Y(TGTOB)]
      
      ' Within fighting distance 
      If Param<=1
         
         Proc _DOENEMY2PLAYERATTACK[IDX,TGTIDX]
         
         ' Retailation
         If _PLAYER_ARMY_SIZE(TGTIDX)>0
            Proc _DOPLAYER2ENEMYATTACK[TGTIDX,IDX]
         End If 
         
         Goto _END
      End If 
      
      L2:
   Next I
   
   
   ' Otherwise, we move randomly to player
   
   
   ' Pick first player living for now 
   For I=0 To 6
      If _ENEMY_ARMY_TYPE(I)<>0 and _ENEMY_ARMY_SIZE(I)>0
         TGTOB=_PLAYER_ARMY_OBJ(I)
         TGTIDX=I
         Exit 
      End If 
   Next I
   
   Proc _DIRECTIONTO[_OBJ_X(OB),_OBJ_Y(OB),_OBJ_X(TGTOB),_OBJ_Y(TGTOB)]
   
   ' Move 
   Proc _OBJMOVE[OB,Param]
   
   
   ' Check to see any living
   LIVING=0
   For I=0 To 6
      LIVING=LIVING+_PLAYER_ARMY_SIZE(I)
   Next I
   
   If LIVING=0
      Print "You lost"
      End 
   End If 
   
   LIVING=0
   For I=0 To 6
      LIVING=LIVING+_ENEMY_ARMY_SIZE(I)
   Next I
   
   If LIVING=0
      Proc _LEAVEBATTLE
   End If 
   
   
   _END:
End Proc

Procedure _GOTOWORLD
   _ISLAND=0
   
   _MAPMODE=1
   _MAPVIEW_X=0
   _MAPVIEW_Y=0
   _MAPVIEW_W=253
   _MAPVIEW_H=256
   _MAP_X=0
   _MAP_Y=0
   
   Proc _LOGTEXT["You start sailing"]
   
   Proc _CLEAROBJECTS
   Proc _ADDOBJECT[K_OBJ_PLAYER,5,5,1]
   
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
End Proc

Procedure _GOTOISLAND[ISLAND]
   _ISLAND=ISLAND
   
   Proc _RESTOREISLAND
   
   ' 0=WORLD MAP
   ' 1 = Rum Island 
   ' 2 = Oyster Cay 
   ' 3 = Barracuda Isle 
   ' 4 = Danger Cove  
   
   If _ISLAND=1
      Proc _LOGTEXT["You've landed at Rum Island!"]
   Else If _ISLAND=2
      Proc _LOGTEXT["You've landed at Oyster Cay"]
   Else If _ISLAND=3
      Proc _LOGTEXT["You've landed at Barracuda Isle"]
   Else 
      Proc _LOGTEXT["You've landed at Danger Cove"]
   End If 
   
   Proc _CLEAROBJECTS
   Proc _ADDOBJECT[K_OBJ_PLAYER,5,5,2]
   Proc _ADDOBJECT[K_OBJ_ENEMY,3,7,2]
   
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
End Proc


Procedure _RESTOREISLAND
   _MAPMODE=2
   _MAP_X=14
   _MAP_Y=0
   _MAPVIEW_X=0
   _MAPVIEW_Y=0
   _MAPVIEW_W=253
   _MAPVIEW_H=256
End Proc

Procedure _SHOWTREASURE
   For I=0 To Map X-1
      For J=0 To Map Y-1
         If Tile Val(I,J,0)=8
            Map Plot 15,I,J
         End If 
      Next J
   Next I
End Proc

Procedure _ADDARMYOBJECT[__IS_PLAYER,__INDEX,__TYPE,__SIZE]
   TY=K_OBJ_PLAYER
   X=0
   _BOBID=2
   If __IS_PLAYER=False
      TY=K_OBJ_ENEMY
      X=7
      _BOBID=2
   End If 
   
   Proc _ADDOBJECT[TY,X,__INDEX,_BOBID]
   
   If __IS_PLAYER=True
      _PLAYER_ARMY_OBJ(__INDEX)=Param
   Else 
      _ENEMY_ARMY_OBJ(__INDEX)=Param
      _ENEMY_ARMY_TYPE(__INDEX)=__TYPE
      _ENEMY_ARMY_SIZE(__INDEX)=__SIZE
   End If 
   
End Proc

Procedure _REMOVEARMYOBJECT[__IS_PLAYER,__INDEX]
   OB=0
   If __ISPLAYER
      OB=_PLAYER_ARMY_OBJ(__INDEX)
      _PLAYER_ARMY_TYPE(__INDEX)=0
      _PLAYER_ARMY_OBJ(__INDEX)=0
      _PLAYER_ARMY_SIZE(__INDEX)=0
   Else 
      OB=_ENEMY_ARMY_OBJ(__INDEX)
      _ENEMY_ARMY_TYPE(__INDEX)=0
      _ENEMY_ARMY_OBJ(__INDEX)=0
      _ENEMY_ARMY_SIZE(__INDEX)=0
   End If 
   
   Proc _REMOVEOBJECT[OB]
   
End Proc

Procedure _ARMYFILLPLAYER
   For I=0 To 6
      If _PLAYER_ARMY_TYPE(I)<>0
         _ADDARMYOBJECT[True,I,0,0]
      End If 
   Next I
End Proc

Procedure _PLAYERADDORJOINARMY[TY,COUNT]
   
   For I=0 To 6
      If _PLAYER_ARMY_TYPE(I)=TY
         _PLAYER_ARMY_SIZE(I)=_PLAYER_ARMY_SIZE(I)+COUNT
         Goto _END
      End If 
   Next I
   
   For I=0 To 6
      If _PLAYER_ARMY_TYPE(I)=0
         _PLAYER_ARMY_TYPE(I)=TY
         _PLAYER_ARMY_SIZE(I)=COUNT
         Goto _END
      End If 
   Next I
   
   _END:
   _LOGTEXTNUM2["We have recruited ",COUNT," men"]
   
End Proc

Procedure _ARMYFILL[OBJ]
   ' Temp; Will need to look this up. 
   _ADDARMYOBJECT[False,0,K_ARMY_PIRATES,5]
End Proc

Procedure _ARMYFILLFORT
   ' Temp; Will need to look this up. 
   _ADDARMYOBJECT[False,0,K_ARMY_PIRATES,20]
End Proc

Procedure _GOTOBATTLE[__EN_OBJ]
   _MAPMODE=3
   _PLAYER_MOVE=0
   _ARMY_TURN=0
   _ARMY_SELECT(0)=0
   _ARMY_SELECT(1)=0
   _FORT_BATTLE=False
   
   Proc _REMOVEOBJECT[__EN_OBJ]
   
   Proc _PUSHOBJECTS
   Proc _CLEAROBJECTS
   
   Proc _ARMYFILLPLAYER
   Proc _ARMYFILL[__EN_OBJ]
   
   Proc _LOGTEXT["You've have encountered some unfriendly individuals"]
   
   
   Cls 0
   _REDRAW=True
End Proc

Procedure _LEAVEBATTLE
   Proc _POPOBJECTS
   Proc _RESTOREISLAND
   
   If _FORT_BATTLE
      Proc _LEAVEFORT
   End If 
   
   Cls 0
   _REDRAW=True
End Proc

Procedure _LEAVEISLAND
   Proc _GOTOWORLD
End Proc

Procedure _ENTERFORT
   _MAPMODE=3
   _PLAYER_MOVE=0
   _ARMY_TURN=0
   _ARMY_SELECT(0)=0
   _ARMY_SELECT(1)=0
   _FORT_BATTLE=True
   
   
   Proc _PUSHOBJECTS
   Proc _CLEAROBJECTS
   
   Proc _ARMYFILLPLAYER
   Proc _ARMYFILLFORT
   
   Proc _LOGTEXT["You've have stormed the fort!"]
   
   Cls 0
   _REDRAW=True
End Proc

Procedure _LEAVEFORT
   _PLAYER_MAP_PIECES=_PLAYER_MAP_PIECES+1
End Proc

Procedure _ENTERTOWN
   _STATE=2
   _UIMODE=2
   _REDRAW=True
   _TOWN_NAME$="Southern Port"
   '_TOWN_TREASURE=True 

End Proc

Procedure _LEAVETOWN
   _STATE=1
   _REDRAW=True
   _TOWN_TREASURE=False

   If _TOWN_ACTION=1
      _PLAYER_GOLD=_PLAYER_GOLD-10
      Proc _SHOWTREASURE
   End If 
   
End Proc

Procedure _ENTERTREASURE
   _STATE=2
   _UIMODE=3
   _REDRAW=True
   _TREASURE_GOLD=100
End Proc

Procedure _LEAVETREASURE
   _PLAYER_GOLD=_PLAYER_GOLD+_TREASURE_GOLD
   _STATE=1
   _REDRAW=True
End Proc

Procedure _ENTERRECRUIT
   _STATE=2
   _UIMODE=1
   _REDRAW=True
   _RECRUIT_TYPE=K_ARMY_PIRATES
   _RECRUIT_COUNT=100
   _RECRUIT_COST=5
   _RECRUIT_ENLIST=0
End Proc

Procedure _LEAVERECRUIT
   _STATE=1
   _REDRAW=True
   
   If _RECRUIT_ENLIST>1
      _PLAYER_GOLD=_PLAYER_GOLD-_RECRUIT_ENLIST*_RECRUIT_COST
      _PLAYERADDORJOINARMY[_RECRUIT_TYPE,_RECRUIT_ENLIST]
   End If 
   
End Proc

'''''''''''''''''''''''''''''''''''''''''''''' 
'
'              STATES
'
'
'''''''''''''''''''''''''''''''''''''''''''''' 

_PLAYSTATE:
On _MAPMODE Gosub _WORLDMAP,_ISLANDMAP,_BATTLEMAP

Return 

_UISTATE:
On _UIMODE Gosub _RECRUITMENT,_TOWN,_TREASURE
Return 


GAMESTART:


_K$=Inkey$
_SC=Scancode

_MX=X Screen(X Mouse)
_MY=Y Screen(Y Mouse)
_MX=Max(0,_MX)
_MY=Max(0,_MY)

SX=_MX
SY=_MY
Proc _SCREEN2TILE
Proc _TILETYPE

If _SC=69
   End 
End If 

On _STATE Gosub _PLAYSTATE,_UISTATE

If _PLAYER_MAP_PIECES=1
   Goto WIN
End If 

Wait Vbl 

Goto GAMESTART

WIN:

Cls 0

_PRINTTROLL[10,10,"Congratulations you are the the best pirate!"]

Wait Key 

GAMEEND:

