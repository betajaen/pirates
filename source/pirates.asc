'
' Pirates Bounty 
' (C) 2018 Robin Southern
' github.com/betajaen
'
K_NB_ICONS=2
K_ARMY_PIRATES=1
K_ARMY_MONKEYS=2
K_ARMY_MERCS=3
K_ARMY_HENCHMEN=4
K_ARMY_VOODOO=5
K_ARMY_GHOSTS=6
K_ARMY_ZOMBIES=7
K_ARMY_SKELETONS=8
K_ARMY_REDCOATS=9

Global K_ARMY_PIRATES,K_ARMY_MONKEYS,K_ARMY_MERCS,K_ARMY_HENCHMEN
Global K_ARMY_VOODOO,K_ARMY_GHOSTS,K_ARMY_ZOMBIES,K_ARMY_SKELETONS
Global K_ARMY_REDCOATS

K_OBJ_PLAYER=1
K_OBJ_ENEMY=2
K_OBJ_TOWN=3
K_OBJ_RECRUIT=4
K_OBJ_FORT=5

Global K_OBJ_PLAYER,K_OBJ_ENEMY,K_OBJ_TOWN,K_RECRUIT,K_OBJ_FORT

K_NORTH=1
K_EAST=2
K_SOUTH=3
K_WEST=4

Global K_NORTH,K_EAST,K_SOUTH,K_WEST


' Redraw the screen according to state 
_REDRAW=False
' Evaluate AI/Next Turn
_EVAL=False

Global _REDRAW,_EVAL

_STATE=1
_X=0
_Y=0
_X1=0
_Y1=0

Global _MAPVIEW_X
Global _MAPVIEW_Y
Global _MAPVIEW_W
Global _MAPVIEW_H
Global _MAP_X
Global _MAP_Y

' 0 = World Map
' 1 = Island Map 
' 2 = Battle Map 
Global _MAPMODE

' 0 = World Map
' 1 = Rum Island 
' 2 = Oyster Cay 
' 3 = Barracuda Isle 
' 4 = Danger Cove  
Global _ISLAND
Global _PLAYER_NAME$
Global _PLAYER_GOLD
Global _PLAYER_REPUTATION
Global _PLAYER_X
Global _PLAYER_Y
Dim _PLAYER_ARMY_TYPE(6),_PLAYER_ARMY_SIZE(6),_PLAYER_ARMY_X(6),_PLAYER_ARMY_Y(6)
Dim _ENEMY_ARMY_TYPE(6),_ENEMY_ARMY_SIZE(6),_ENEMY_ARMY_X(6),_ENEMY_ARMY_Y(6)
Dim _OBJ_TYPE(32),_OBJ_BOB(32),_OBJ_TIMER(32),_OBJ_X(32),_OBJ_Y(32),_OBJ_FLAGS(32)

Global _OBJ_TYPE(),_OBJ_X(),_OBJ_Y(),_OBJ_BOB(),_OBJ_FLAGS(),_OBJ_TIMER()

'Default Palette $FFF,$0,$F00,$F0,$F 
Screen Open 0,320,256,8,Lowres
Curs Off 
Flash Off 

Cls 5

Load "Pirates:resources/foreground.abk"


' Load main 16x16 sprites
Load "Pirates:resources/tiles.abk"
Get Icon Palette 

' Make the "pink transparent" as a white 
Palette $FFF,


' Load the map 
L=Map Length(100,100)
Reserve As Data 6,L
Bload "Pirates:resources/map.map",Start(6)

Reserve As Data 8,2048
Bload "Pirates:resources/map.map.val",Start(8)

' Set map view port
Tile Size 16,16

_PLAYER_NAME$="Blackbeard"
_PLAYER_GOLD=1000
_PLAYER_REPUTATION=1
_PLAYER_X=0
_PLAYER_Y=0

_PLAYER_ARMY_TYPE(0)=K_ARMY_PIRATES
_PLAYER_ARMY_SIZE(0)=5

Proc _GOTOISLAND[1]

Goto GAMESTART


' World Map
_WORLDMAP:

' If Island selected 
If _TV<>0 and Mouse Click=1
   Proc _GOTOISLAND[_TV]
End If 

Return 

' Island Map 
_ISLANDMAP:

' Num Keys 
'62
'45 47 
'30
If _SC=62 Then _OBJMOVE[0,K_NORTH]
If _SC=45 Then _OBJMOVE[0,K_WEST]
If _SC=47 Then _OBJMOVE[0,K_EAST]
If _SC=30 Then _OBJMOVE[0,K_SOUTH]
' F1 
If _SC=80
   _REDRAW=True
End If 

If _EVAL=True
   _REDRAW=True
   _EVAL=False

   Proc _UPDATEAI
End If 

If _REDRAW=True
   _REDRAW=False
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
End If 


Return 

' Battle Map 
_BATTLEMAP:

Return 

Procedure _DRAWTILES
   Shared _MAPVIEW
   
   Map View _MAPVIEW_X,_MAPVIEW_Y To _MAPVIEW_X+_MAPVIEW_W,_MAPVIEW_Y+_MAPVIEW_H
   Map Do _MAP_X,_MAP_Y
End Proc

Procedure _DRAWOBJECTS
   For I=0 To 32
      If _OBJ_TYPE(I)>0
         Paste Bob _OBJ_X(I)*16,_OBJ_Y(I)*16,_OBJ_BOB(I)
      End If 
   Next I
End Proc

Procedure _CLEAROBJECTS
   For I=0 To 32
      _OBJ_TYPE(I)=0
   Next I
End Proc

Procedure _ADDOBJECT[__OBJTYPE,__X,__Y,__BOB]
   ID=99
   For I=0 To 32
      If _OBJ_TYPE(I)=0
         _OBJ_TYPE(I)=__OBJTYPE
         _OBJ_BOB(I)=__BOB
         _OBJ_X(I)=__X
         _OBJ_Y(I)=__Y
         ID=I
         _REDRAW=True
         Exit 
      End If 
   Next I
End Proc[ID]

Procedure _REMOVEOBJECT[__ID]
   _OBJ_TYPE(__ID)=0
   _REDRAW=True
End Proc[ID]

___MOVE_COORDS_X=0
___MOVE_COORDS_Y=0

Global ___MOVE_COORDS_X,___MOVE_COORDS_Y

Procedure _MOVECOORDS[__ID,__DIR]
   ___MOVE_COORDS_X=_OBJ_X(__ID)
   ___MOVE_COORDS_Y=_OBJ_Y(__ID)
   
   If __DIR=K_NORTH
      ___MOVE_COORDS_Y=___MOVE_COORDS_Y-1
   Else If __DIR=K_EAST
      ___MOVE_COORDS_X=___MOVE_COORDS_X+1
   Else If __DIR=K_SOUTH
      ___MOVE_COORDS_Y=___MOVE_COORDS_Y+1
   Else If __DIR=K_WEST
      ___MOVE_COORDS_X=___MOVE_COORDS_X-1
   End If 
   
End Proc

Procedure _DIRECTIONTO[__SX,__SY,__TX,__TY]
   _DIR=0


   If __SX>__TX
      _DIR=K_WEST
   Else If __SX<__TX
      _DIR=K_EAST
   Else If __SY>__TY
      _DIR=K_NORTH
   Else If __SY<__TY
      _DIR=K_SOUTH
   End If 

End Proc[_DIR]


Procedure _CANMOVE[__ID,__TX,__TY]
   CAN_MOVE=True
End Proc[CAN_MOVE]

Procedure _OBJMOVE[__ID,__DIR]
   Proc _MOVECOORDS[__ID,__DIR]
   Proc _CANMOVE[__ID,___MOVE_COORDS_X,___MOVE_COORDS_Y]
   If Param=True
      _OBJ_X(__ID)=___MOVE_COORDS_X
      _OBJ_Y(__ID)=___MOVE_COORDS_Y
      _REDRAW=True
      _EVAL=True
   End If 
End Proc

Procedure _UPDATEAI
   PX=_OBJ_X(0)
   PY=_OBJ_Y(0)

   For I=1 To 32
      If _OBJ_TYPE(I)=K_OBJ_ENEMY
         Proc _DIRECTIONTO[_OBJ_X(I),_OBJ_Y(I),PX,PY]
         If Param<>0
            Proc _OBJMOVE[I,Param]
         End If 
      End If 
   Next I
   ' Prevent eval loop
         _EVAL=False
End Proc

Procedure _GOTOWORLD
   _ISLAND=0
   _MAPMODE=1
   _MAPVIEW_X=0
   _MAPVIEW_Y=0
   _MAPVIEW_W=208
   _MAPVIEW_H=160
   _MAP_X=0
   _MAP_Y=0
   
   Proc _CLEAROBJECTS
   Proc _ADDOBJECT[K_OBJ_PLAYER,5,5,1]
   
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
   
End Proc

Procedure _GOTOISLAND[ISLAND]
   _ISLAND=ISLAND
   _MAPMODE=2
   _MAP_X=16
   _MAP_Y=0
   _MAPVIEW_X=0
   _MAPVIEW_Y=0
   _MAPVIEW_W=320
   _MAPVIEW_H=256
   
   Proc _CLEAROBJECTS
   Proc _ADDOBJECT[K_OBJ_PLAYER,5,5,2]
   Proc _ADDOBJECT[K_OBJ_ENEMY,10,5,1]
   
   Proc _DRAWTILES
   Proc _DRAWOBJECTS
End Proc



'''''''''''''''''''''''''''''''''''''''''''''' 
'
'              STATES
'
'
'''''''''''''''''''''''''''''''''''''''''''''' 

_PLAYSTATE:
On _MAPMODE Gosub _WORLDMAP,_ISLANDMAP,_BATTLEMAP

Return 

STATE2:
Print "State 2"
Return 


GAMESTART:
On _STATE Gosub _PLAYSTATE,STATE2
_K$=Inkey$
_SC=Scancode

_MX=X Screen(X Mouse)
_MY=Y Screen(Y Mouse)
_MX=Max(0,_MX)
_MY=Max(0,_MY)
_TX=_MX/16
_TY=_MY/16

Locate 30,0
Print _STATE,",",_MAPMODE

Locate 0,0
Print _MX,",",_MY

_TV=Tile Val(_TX,_TY,7)
Locate 0,1
Print _TV


' Alpha 1
If _K$="o"
   _STATE=1
Else If _K$="p"
   _STATE=2
   ' Escape -> Quit 
Else If _SC=69
   Stop 
   End 
End If 

Goto GAMESTART


GAMEEND:

